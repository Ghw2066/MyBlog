---
title: "汇编学习笔记（四）---栈、函数、递归"
date: 2019-10-14T10:15:04+11:00
draft: false
categories:
 - 计算机系统
tags:
 - 汇编Assembly
 - 递归
---

## 先吐个槽

前面几个实验虽然或多或少碰到点小麻烦，但是编程的思路还是比较清晰，按照逻辑一步一步来就好了，无非就是相比高级编程语言麻烦一些，直到遇到了本次lab。。

在lecture上首先介绍了`栈(stack)`的概念，然后引入用`栈(stack)`实现的`函数(function)`，讲到这里我已经晕了，各种`pointer`满天飞，然后就开始用`rcall` `ret`开始实现递归了，心里一万头草泥马。。。

吐槽归吐槽，lab还得做，日子还得过

## 任务描述

> GCD can be calculated in many ways. Figure 1 shows a different approach (as compared to the one used in Lab 1) to obtain GCD of two integers (written in C,  where, `a%b` is the remainder of `a` divided by `b`). 
>
> Write an assembly recursive function to implement the C program. Here we assume the integer size is two bytes. For readability, use a macro for `n1%n2` in your assembly code.
>
> > int gcd(int a, int b) 
> > {    
> > 	if (b != 0)        
>    > 		return gcd(b, a%b);   
>    > 	else
>    >        return a;
>    > } 
> 
> Assemble your program using AVR Studio, and run it on the AVR Microcontroller Board. Demonstrate your working program to the lab assessor.  
>

## 解题思路

其实在课后仔细学习了lecture N+1遍后，终于顿悟。虽然各种指令集看起来花里胡哨的，但其实还是逻辑清晰，条例明确的，只要真正理解了栈用于函数的实现以及各个指令集的功能，这个问题也就迎刃而解了。

####  mod 求余的实现

首先指令集中并没有求余指令，所以这里要自己实现一个，最后保存在一个macro中，方便调用，同时也增强了程序的可读性与健壮性。这里实现如下：

```assembly
.macro mod		;a%b a->@1:@0   b->@3@2	
loop:
	cp @0,@2	
	cpc @1,@3
	brsh sbtr
	rjmp done
sbtr:
	sub @0,@2
	sbc @1,@3	
	rjmp loop
done:
.endmacro
```



####  `gcd`函数的prologue

这个prologue不知道怎么翻译合适，我理解的就是相当于c语言中的函数头部分，定义了与外界的接口，将值传递进入函数内部。

```c
int gcd(int a, int b)
```

因为汇编中也没有函数的实现，所以要使用栈这种结构实现一个函数。

因此需要`stack pointer`以及`stack frame pointer`来实现一个栈，前者是汇编中已有的栈指针，为2字节，表示为`SPL`和`SPH`；后者是根据我们的需要，需要使用`Y`，即`R29:R28`寄存器对，作为`frame pointer`，用来分配内存以及指向栈顶。

**Prologue**

```assembly
;prologue
				;Y -> r29:r28 will be used as the frame pointer
	push YL		;save r29:r28 in the stack
	push YH		
	push r16
	push r17
	push zero
	in YL,SPL
	in YH,SPH
	sbiw Y,4

	out SPH,YH	;update the stack pointer to 
	out SPL,YL	;point to the new stack top

	std Y+1,al	;pass a's value to local_a in the function
	std Y+2,ah
	std Y+3,bl	;pass b's value to local_b in the function
	std Y+4,bh
	;End of prologue
```

####  `gcd`函数的`function body`

函数主体部分就比较直观了，基本就是对c代码的汇编实现。

**Function body**

```assembly
;function body
	clr zero
	cp r20,zero
	cpc r21,zero
	brne L1		;if(b!=0) branch to L1: gcd(b, a%b)

	rjmp L2		;else jump to L2, return a
L1:
	ldd al,Y+3	;load b to a
	ldd ah,Y+4
	ldd bl,Y+1	;load a to b
	ldd bh,Y+2
	;swap the value of a,b
	movw r16:r17,al:ah		;copy b_value to r16:r17
	mod bl,bh,r16,r17		;calculate a%b and put the result in b
	;now a is b, b is a%b
	rcall gcd				;return gcd(b, a%b);
```

####  `gcd`函数的`epilogue`

这个epilogue我理解为将栈释放，返回值，所以这里的一系列操作几乎就是`Prologue`的反向操作。

**Epilogue**

```assembly
L2:
	adiw Y,4		;deallocate the reserved space
	out SPH,YH
	out SPL,YL
	pop zero		
	pop r17			;restore registers
	pop r16
	pop YH
	pop YL
	ret				;return to main()
	;End of epilogue
```

## 完整代码

```assembly
;
; task1_gcd_recursive.asm
;
; Created: 2019/10/10 11:43:21
; Author : GHW
;
;int gcd(int a, int b) 
;{    
;	if (b != 0)        
;		return gcd(b, a%b);   
;	else
;       return a;
;} 
 

; Replace with your application code
.include "m2560def.inc"
.def zero = r15			;to store constant value 0
.def al = r22
.def ah = r23
.def bl = r20
.def bh = r21

.macro mod		;a%b a->@1:@0   b->@3@2	
loop:
	cp @0,@2	
	cpc @1,@3
	brsh sbtr
	rjmp done
sbtr:
	sub @0,@2
	sbc @1,@3	
	rjmp loop
done:
.endmacro

.CSEG			;start code segment

main:
	;mod ah,al,bh,bl,r25,r24
	ldi al,low(3500)
	ldi ah,high(3500)
	ldi bl,low(500)
	ldi bh,high(500)
	rcall gcd
	;movw r23:r22,r25:r24
end:
	rjmp end

gcd:
	;prologue
				;Y -> r29:r28 will be used as the frame pointer
	push YL		;save r29:r28 in the stack
	push YH		
	push r16
	push r17
	push zero
	in YL,SPL
	in YH,SPH
	sbiw Y,4

	out SPH,YH	;update the stack pointer to 
	out SPL,YL	;point to the new stack top

	std Y+1,al	;pass a's value to local_a in the function
	std Y+2,ah
	std Y+3,bl	;pass b's value to local_b in the function
	std Y+4,bh
	;End of prologue

	;function body
	clr zero
	cp r20,zero
	cpc r21,zero
	brne L1		;if(b!=0) branch to L1: gcd(b, a%b)

	rjmp L2		;else jump to L2, return a
L1:
	ldd al,Y+3	;load b to a
	ldd ah,Y+4
	ldd bl,Y+1	;load a to b
	ldd bh,Y+2
	;swap the value of a,b
	movw r16:r17,al:ah		;copy b_value to r16:r17
	mod bl,bh,r16,r17		;calculate a%b and put the result in b
	;now a is b, b is a%b

	rcall gcd				;return gcd(b, a%b);

L2:
	adiw Y,4		;deallocate the reserved space
	out SPH,YH
	out SPL,YL
	pop zero		
	pop r17			;restore registers
	pop r16
	pop YH
	pop YL
	ret				;return to main()
	;End of epilogue
```

## 小结

并不是第一次接触递归，过去学`c/c++`,` java`, `python`都接触过递归，但是其实心里一直对递归有种知其然不知其所以然的感觉，直到这次用汇编手动操作内存，用栈实现一个函数，同时实现递归之后，才终于算是理解递归是一个什么样的过程，在每一次递归的过程中，函数都要在内存中申请新的空间，存放本次递归的结果，直到递归达到返回条件之后，再逐层返回，并释放内存。所以说递归其实是一个内存开销比较大的方法，但熟练掌握之后，确实是更符合编程逻辑的方式。